#include <cstdio>
#include <iostream>
#include <cuda_runtime.h>
#include "kernel.cuh"

/**
 * 计算一层内到某个海拔水平面的所有可能走时
 */
__global__ void
cudaRayKernel(float *vel, float *thick, float *rho, float *travel_time, float *incident,
              float *epicentral_distance, int n_layers)
{
    /* get current thread's id */
    int index = blockIdx.x * blockDim.x + threadIdx.x;
    // printf("\ncurrent id:%d", index);
    // printf("dev_l_rho:%d\n", dev_l_rho);

    /* while this thread is dealing with a valid index */
    if (index < dev_l_rho)
    {
        float r = rho[index]; // 水平慢度
        float tr_time = 0.0f;
        float ep_dist = 0.0f;
        for (int k = 0; k < n_layers; k++)
        {
            // Calculate travel time with rho
            tr_time += thick[k] / (vel[k] * sqrt(1 - (r * r * vel[k] * vel[k])));
            ep_dist += (r * vel[k] * thick[k]) / sqrt(1 - (r * r * vel[k] * vel[k]));
        }

        // 走时结果，存储到全局数组
        travel_time[index] = tr_time;
        // 震中距，存储到全局数组
        epicentral_distance[index] = ep_dist;
        // 计算、存储入射角的弧度值
        incident[index] = asin(r * vel[0]);
        // printf("\ntime:%f,dist:%f,angle:%f", tr_time, ep_dist, asin(r * vel[n_layers - 1]) * 180);
        return;
    }
}

void cudaCallRayKernel(const unsigned int blocks,
                       const unsigned int threadsPerBlock, // 每块线程数量
                       float *vel,                         // input每层速度
                       float *thick,                       // input每层厚度
                       float *rho,                         // input慢度水平分量
                       float *travel_time,                 // output走时
                       float *incident,                    // output方位角
                       float *epicentral_distance,         // output震中距
                       int n_layers                        // 地层数量
)
{

    // Call the kernel above this function.
    cudaRayKernel<<<blocks, threadsPerBlock>>>(vel, thick, rho, travel_time, incident,
                                               epicentral_distance, n_layers);
}

/**
 * 寻找一层内每个网格点到每个站的最逼近射线
 */
__global__ void cudaFindKernel(unsigned int nx,
                               unsigned int ny,
                               unsigned int nsta,
                               float *travel_time,         // input
                               float *incident,            // input
                               float *epicentral_distance, // input
                               float *staxy_ed,            // input
                               float *staxy_tt,            // output
                               float *staxy_incident,      // output
                               float *staxy_err            // output
)
{
    /* get current thread's id */
    // index直接是staxy数组下标
    int index = blockIdx.x * blockDim.x + threadIdx.x;
    // printf("\ncurrent id:%d", index);

    /* while this thread is dealing with a valid index */
    if (index < nx * ny * nsta)
    {
        float real_ed = staxy_ed[index];
        float best_err = FLT_MAX;
        float best_tt, best_incident;
        // 遍历寻找最小err，记录对应的tt,incident,err到数组
        for (int k = 0; k < dev_l_rho; ++k)
        {
            float err = abs(epicentral_distance[k] - real_ed);
            // 更新最小误差和相关数据
            if (abs(epicentral_distance[k] - real_ed) < best_err)
            {
                best_err = err;
                best_tt = travel_time[k];
                best_incident = incident[k];
            }
        }
        // 将结果写入输出数组
        staxy_tt[index] = best_tt;
        staxy_incident[index] = best_incident;
        staxy_err[index] = best_err;
        return;
    }
}

void cudaCallFindKernel(
    const unsigned int blocks,
    const unsigned int threadsPerBlock,
    unsigned int nx,
    unsigned int ny,
    unsigned int nsta,
    float *travel_time,
    float *incident,
    float *epicentral_distance,
    float *staxy_ed,
    float *staxy_tt,
    float *staxy_incident,
    float *staxy_err)
{
    cudaFindKernel<<<blocks, threadsPerBlock>>>(nx, ny, nsta, travel_time, incident,
                                                epicentral_distance, staxy_ed,
                                                staxy_tt, staxy_incident, staxy_err);
}

// 使用 CUDA 内核的函数来设置设备全局变量
__global__ void cudaUpdateLenRho(int new_value)
{
    dev_l_rho = new_value;
    printf("dev_l_rho change to:%d\n", dev_l_rho);
}
void update_dev_l_rho(int l_rho)
{
    cudaUpdateLenRho<<<1, 1>>>(l_rho);
    cudaDeviceSynchronize();
}